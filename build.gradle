plugins {
  id 'java'
  id 'org.openapi.generator' version '7.13.0'
  id 'com.diffplug.spotless' version '7.0.3'
  id 'jacoco'
  id 'maven-publish'
  id "com.github.ben-manes.versions" version "0.52.0"
  id "org.cyclonedx.bom" version "2.3.0"
}

group = 'uk.gov.hmcts.cp'
version = System.getProperty('API_SPEC_VERSION') ?: '0.0.999'
def githubActor = project.findProperty("github.actor") ?: System.getenv("GITHUB_ACTOR")
def githubToken = project.findProperty("github.token") ?: System.getenv("GITHUB_TOKEN")
def githubRepo = System.getenv("GITHUB_REPOSITORY")

//debugging
//    if (githubActor != null) {
//      println "ðŸ” Configuring GitHub Packages publishing to: https://maven.pkg.github.com/$githubRepo"
//    }
//    println "GitHub Packages publishing required environment variables:"
//    println " - GITHUB_ACTOR=${githubActor != null ? 'âœ”' : 'âŒ'}"
//    println " - GITHUB_TOKEN=${githubToken != null ? 'âœ”' : 'âŒ'}"
//    println " - GITHUB_REPOSITORY=${githubRepo != null ? 'âœ”' : 'âŒ'}"

java {
  sourceCompatibility = JavaVersion.VERSION_21
  targetCompatibility = JavaVersion.VERSION_21
}

sourceSets {
  main {
    java {
      srcDir "$buildDir/generated/src/main/java"
    }
  }
}

tasks.withType(JavaCompile).configureEach {
  options.compilerArgs = ["-Xlint:unchecked", "-Werror"]
}

tasks.withType(Test).configureEach {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

tasks.named('test') {
  useJUnitPlatform()
  systemProperty 'API_SPEC_VERSION', System.getProperty('API_SPEC_VERSION', '0.0.0')
  failFast = true
  testLogging {
    events "passed", "skipped", "failed"
    exceptionFormat = 'full'
    showStandardStreams = true
  }
}

tasks.named('jacocoTestReport') {
  dependsOn tasks.named('test')
  reports {
    xml.required.set(true)
    csv.required.set(false)
    html.required.set(true)
  }
}

tasks.named('check') {
  dependsOn tasks.named('jacocoTestReport')
}

// check dependencies upon release ONLY
tasks.named("dependencyUpdates").configure {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf {
    isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url = 'https://jitpack.io'
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
    }
  }
  repositories {
    maven {
      name = "GitHubPackages"
      url = uri("https://maven.pkg.github.com/$githubRepo")
      credentials {
        username = githubActor
        password = githubToken
      }
    }
  }
}

//Creation of Software Bill of Materials
//https://github.com/CycloneDX/cyclonedx-gradle-plugin
cyclonedxBom {
  projectType = "library" // or application
  includeConfigs = ["runtimeClasspath"]
  skipConfigs = ["test", "testImplementation"]
  schemaVersion = "1.6"
  componentVersion = providers.provider { project.version.toString() }
  destination = file("$buildDir/reports")
  includeBuildSystem = true
  outputName = "bom"
  outputFormat = "json"
}

jar {
  dependsOn tasks.named('cyclonedxBom')
  if (file("CHANGELOG.md").exists()) {
    println "âœ… Including CHANGELOG.md in JAR"
    from('CHANGELOG.md') {
      into 'META-INF'
    }
  } else {
    println "âš ï¸  CHANGELOG.md not found, skipping inclusion in JAR"
  }

  def sbomFile = file("$buildDir/reports/bom.json")
  if (sbomFile.exists()) {
    println "âœ… Including SBOM from ${sbomFile} in JAR"
    from(sbomFile) {
      into 'META-INF/sbom'
      rename { 'bom.json' }
    }
  } else {
    println "âš ï¸  SBOM (bom.json) not found, skipping inclusion in JAR"
  }
}

def inputSpecFile = fileTree("src/main/resources/openapi").matching {
  include("*.openapi.yml")
}.singleFile

tasks.named("openApiGenerate") {
  inputs.file(inputSpecFile)
}

openApiGenerate {
  generatorName = "spring"
  inputSpec = inputSpecFile.absolutePath
  outputDir = "$buildDir/generated"
  apiPackage = "uk.gov.hmcts.cp.openapi.api"
  modelPackage = "uk.gov.hmcts.cp.openapi.model"
  generateModelTests = true
  generateApiTests = true
  cleanupOutput = true
  configOptions = [
          validateSpec                  : "true",
          dateLibrary                   : "java8",
          interfaceOnly                 : "true",
          hideGenerationTimestamp       : "true",
          useJakartaEe                  : "true",
          useBeanValidation             : "true",
          useTags                       : "true",
          useSpringBoot3                : "true",
          implicitHeaders               : "false",
          performBeanValidation         : "true",
          additionalModelTypeAnnotations: "@lombok.Builder;@lombok.AllArgsConstructor;@lombok.NoArgsConstructor",
          serializableModel             : "true",
          openApiNullable               : "false"
  ]
}

tasks.named('compileJava') {
  dependsOn tasks.named('spotlessApply')
}

def formatterUrl = 'https://raw.githubusercontent.com/hmcts/api-cp-code-style/refs/heads/master/config/formatter/eclipse-formatter.xml'
def formatterPath = "$buildDir/eclipse-formatter.xml"
tasks.register('downloadFormatter') {
  outputs.file(formatterPath)
  doLast {
    def file = new File(formatterPath)
    file.parentFile.mkdirs()
    file.text = new URI(formatterUrl).toURL().text
  }
}
spotless {
  java {
    target 'build/generated/src/main/**/*.java'
    removeUnusedImports()
    eclipse().configFile(formatterPath)
  }
}

tasks.named('spotlessApply') {
  dependsOn tasks.named('openApiGenerate'), tasks.named('downloadFormatter')
}
tasks.named('spotlessJava') {
  dependsOn tasks.named('openApiGenerate'), tasks.named('downloadFormatter')
}

tasks.withType(Checkstyle).configureEach {
  def generatedDir = file("${buildDir}/generated/src/main/java").canonicalPath
  source = source.filter { file ->
    !file.canonicalPath.startsWith(generatedDir)
  }
}

ext {
  springBootVersion = "3.4.5"
  lombokVersion = "1.18.38"
}

dependencies {
  // OpenAPI Generator runtime support
  implementation 'org.openapitools:openapi-generator-core:7.13.0'
  implementation 'io.swagger.parser.v3:swagger-parser:2.1.26'
  implementation 'io.swagger.core.v3:swagger-annotations:2.2.30'
  implementation 'net.logstash.logback:logstash-logback-encoder:8.1'

  implementation 'com.fasterxml.jackson.core:jackson-annotations:2.19.0'
  implementation "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
  implementation "org.springframework.boot:spring-boot-starter-validation:$springBootVersion"
  compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

  testImplementation(platform('org.junit:junit-bom:5.12.2'))
  testImplementation 'org.junit.jupiter:junit-jupiter-api'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}